

layout(origin_upper_left) in vec4 gl_FragCoord;

in flat s_transform v_t;
in flat vec3 v_color;
in vec2 v_uv;
in vec2 v_local_uv;


out vec4 out_color;

uniform vec2 window_size;
uniform float time;

layout (location = 0) uniform sampler2D in_texture;

vec2 n22(vec2 p)
{
	vec3 a = fract(p.xyx * vec3(123.34, 234.34, 345.65));
	a += dot(a, a + 34.45);
	return fract(vec2(a.x * a.y, a.y * a.z));
}

float sin2(float s)
{
	return sin(s) * 0.5 + 0.5;
}

vec4 pma(vec4 col)
{
	return vec4(col.rgb * col.a, col.a);
}

bool point_in_rect_topleft(vec2 point, vec2 pos, vec2 size)
{
	return point.x >= pos.x && point.x <= pos.x + size.x && point.y >= pos.y && point.y <= pos.y + size.y;
}

vec3 circle(vec2 pos, vec2 uv, float radius)
{
	vec2 ar = vec2(1.0, window_size.y / window_size.x);
	pos *= ar;
	uv *= ar;
	float d = distance(pos, uv);
	d = smoothstep(radius, 0.0, d);
	// return vec3(d * 0.1, d * 0.4, d);
	return vec3(d);
}

void main()
{
	s_transform t = v_t;

	if(t.do_clip != 0)
	{
		// vec2 pos = t.pos + t.draw_size * v_local_uv;
		if(!point_in_rect_topleft(gl_FragCoord.xy, t.clip_pos, t.clip_size)) { discard; }
	}

	vec4 color;
	if(t.texture_id != 0)
	{
		color = texture(in_texture, v_uv) * pma(t.color);
	}
	else
	{
		color = t.color;
	}

	color.rgb = mix(color.rgb, t.mix_color.rgb, t.mix_weight);

	if(t.do_circle != 0)
	{
		float d = distance(v_local_uv, vec2(0.5));
		color.a *= smoothstep(0.499, 0.45, d);
	}
	else if(t.do_light != 0)
	{
		float d = distance(v_local_uv, vec2(0.5));
		vec3 col = mix(vec3(1.0), color.rgb, smoothstep(0.0, 0.1, d));
		color.rgb = col;
		color.a *= smoothstep(0.499, 0.45, pow(d, 0.4));
	}
	else if(t.background_id == 1)
	{
		vec2 uv = v_local_uv;
		vec3 col = vec3(0);

		float r = 1.0;
		uv *= r;

		for(int i = 0; i < 50; i += 1)
		{
			int a = (i*3+2137);
			vec2 seed = vec2(a, (i*7+123) * 2);
			vec2 rand_pos = n22(seed) * r;
			col += circle(rand_pos, uv, sin2(time * 0.5 + a) * 0.15 + 0.1) * vec3(0.1, 0.4, 1.0);
		}
		color.rgb = col * 0.1;
	}
	else if(t.background_id == 2)
	{
		vec2 uv = v_local_uv;
		vec3 col = vec3(0);

		float r = 1.0;
		uv *= r;

		for(int i = 0; i < 50; i += 1)
		{
			int a = (i*3+1234);
			vec2 seed = vec2(a, (i*6+123) * 3);
			vec2 rand_pos = n22(seed) * r;
			col += circle(rand_pos, uv, sin2(time * 4.0 + a) * 0.15 + 0.1) * vec3(1.0, 0.25, 0.15);
		}
		color.rgb = col * 0.12;
	}
	else if(t.background_id == 3)
	{
		vec2 uv = v_local_uv;
		vec3 col = vec3(0);

		float r = 1.0;
		uv *= r;

		for(int i = 0; i < 50; i += 1)
		{
			int a = (i*3+1234);
			vec2 seed = vec2(a, (i*6+123) * 3);
			vec2 rand_pos = n22(seed) * r;
			col += circle(rand_pos, uv, sin2(time * 4.0 + a) * 0.15 + 0.1) * v_color;
		}
		color.rgb = col * 0.12;
	}
	color = pma(color);

	if(color.a <= 0) { discard; }

	out_color = color;
}

